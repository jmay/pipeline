#!/usr/bin/env perl

use strict;

use Getopt::Long;
use IO::Handle;
use IO::Wrap;
use Text::CSV_XS;
use YAML::Syck;

=head1 NAME

csv2tsv - Convert comma-separated to tab separated

=head1 DESCRIPTION

Reads CSV from stdin, writes tab-separated (TSV) to stdout

Skips blank lines

Strips leading and trailing whitespace from all fields

=head1 OPTIONS

  --delim delim [OPTIONAL] expect delim in the source instead of comma
  --eol eol [OPTIONAL] specifies end-of-line string (options are 'cr', 'lf' ,'crlf')

=head1 CONTRACT

Expects input in CSV

Expects input to have no empty lines

Expects input fields to have been stripped of leading & trailing whitespace

Expects input in UTF-8

Writes UTF-8 as output

Output will have no blank lines

=head1 REQUIRED MODULES

This script requires the following modules:

	Text::CSV_XS

=head1 TODO

Compress any whitespace (spaces, tabs, newlines) in every field to a single space (so that the expectation
  of a normalized source can be that every line is a row and there are no embedded tabs)

Ensure that every output line has the same number of fields (either make this an expectation on the source,
  or pad the output, or require a command-line parameter)

=cut

my $USAGE = "usage: csv2tsv [--delim delim] [--headers n] [--footers n] [--eol eol]";
my ($delimiter, $headers, $footers, $eol);
GetOptions(
  'delim=s' => \$delimiter,
  'eol=s' => \$eol,
  'headers=i' => \$headers,
  'footers=i' => \$footers);
$headers ||= 0;
$footers ||= 0;

# source can have a variety of line terminators
# this is the best way I've found to catch the possibilities I've encountered so far
if ($eol) {
  $eol = "\015" if $eol eq 'cr';
  $eol = "\012" if $eol eq 'lf';
  $eol = "\015\012" if $eol eq 'crlf';
  $/ = $eol;
}

my $csv_in_options = {
  binary => 1,
  sep_char => $delimiter || ',',
  eol => $/,
};
my $csv_out_options = {
  binary => 1,
  sep_char => "\t",
  eol => "\n",
  quote_char => '', # no quoting in the TSV output; all whitespace is crushed so no embedded delims
};

my $csv_in = Text::CSV_XS->new($csv_in_options);
my $csv_out = Text::CSV_XS->new($csv_out_options);

binmode STDIN, ":utf8";
binmode STDOUT, ":utf8";
binmode STDERR, ":utf8";

my (@rows, %ncolumns);

my $io = wraphandle(\*STDIN);
while (my $row = $csv_in->getline($io)) {
  # strip leading and trailing whitespace, compress internal whitespace to single space
  my @fields = map { s/\s+/ /g; $_ } map { s/^\s*|\s*$//g; $_ } @$row;

  my @nonblank_fields = grep { ! /^$/ } @fields;
  if ($#nonblank_fields > 0) {
    # write output only if there is at least one field with data
    push @rows, \@fields;
    $ncolumns{scalar(@fields)}++;
  }
}

my @headers = splice(@rows, 0, $headers);
my @footers = splice(@rows, -$footers, $footers);

# write the execution statistics to stderr
my $results = {
  ':headers' => \@headers,
  ':footers' => \@footers,
  ':nrows' => scalar(@rows),
  # ':rejected_rows' => scalar(@errors),
  ':ncolumns' => \%ncolumns,
};
print STDERR Dump($results);

# write the data records to stdout
for my $row (@rows) {
  if ($csv_out->combine(@$row)) {
    print $csv_out->string;
  }
}
